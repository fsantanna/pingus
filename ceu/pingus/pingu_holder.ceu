#ifndef _PINGUHOLDER_CEU
#define _PINGUHOLDER_CEU

native do
    ##include "../src/pingus/pingu_holder.hpp"
end
native @plain _PinguHolder;

input (int,int,_Framebuffer&&) PINGUHOLDER_RENDER;

input (void&& &&,int,_Vector3f&&,int) PINGUHOLDER_CREATE_PINGU;

input int&& PINGUHOLDER_GET_NUMBER_OF_EXITED;
input int&& PINGUHOLDER_GET_NUMBER_OF_KILLED;
input int&& PINGUHOLDER_GET_NUMBER_OF_ALIVE;
input int&& PINGUHOLDER_GET_NUMBER_OF_RELEASED;
input int&& PINGUHOLDER_GET_NUMBER_OF_ALLOWED;

#include "pingu.ceu"

class PinguHolder with
    interface IPinguHolder;
    var int number_of_exited   = 0;
    var int number_of_killed   = 0;
    var int number_of_alive    = 0;
    var int number_of_released = 0;
    var int number_of_allowed;//=0;

    pool IPingu[] pingus;
    var _PinguHolder& me;
do
    par do
        every ret in PINGUHOLDER_GET_NUMBER_OF_EXITED do
            *ret = this.number_of_exited;
        end
    with
        every ret in PINGUHOLDER_GET_NUMBER_OF_KILLED do
            *ret = this.number_of_killed;
        end
    with
        every ret in PINGUHOLDER_GET_NUMBER_OF_ALIVE do
            *ret = this.number_of_alive;
        end
    with
        every ret in PINGUHOLDER_GET_NUMBER_OF_RELEASED do
            *ret = this.number_of_released;
        end
    with
        every ret in PINGUHOLDER_GET_NUMBER_OF_ALLOWED do
            *ret = this.number_of_allowed;
        end
    with
        every (me_,id,pos,owner_id) in PINGUHOLDER_CREATE_PINGU do
            if this.number_of_released < this.number_of_allowed then
                spawn Pingu in this.pingus with
                    this.holder   = &outer;
                    this.id       = id;
                    this.owner_id = owner_id;
                    this.pos_x    = pos:x;
                    this.pos_y    = pos:y;
                    *me_ = (void&&)&&this;
                end;
            end
        end
    end
end

#endif
