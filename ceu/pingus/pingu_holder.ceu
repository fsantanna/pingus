#ifndef _PINGUHOLDER_CEU
#define _PINGUHOLDER_CEU

#include "pingu.ceu"

class PinguHolder with
    pool IPingu[]& pingus;
    var PingusCounter& counter;
do
    par do
        var int id_total = 0;
        every (x_,y_,dir_,owner_id_) in global:go_create_pingu do
            if counter.number_of_released < counter.number_of_allowed then
                id_total = id_total+1;
                counter.number_of_released = counter.number_of_released + 1;
                counter.number_of_alive    = counter.number_of_alive    + 1;
                var Rect r = Rect(x_,y_,0,0);
                spawn Pingu in this.pingus with
                    this.pingus    = &outer.pingus;
                    this.id        = id_total;
                    this.owner_id  = owner_id_;
                    this.rect      = r;
                    this.direction = dir_;
                end;
            end
        end
    with
        every status in global:ok_pingu do
            if status == _PS_DEAD then
//_assert(0);
                counter.number_of_killed = counter.number_of_killed + 1;
            else/if status == _PS_EXITED then
                counter.number_of_exited = counter.number_of_exited + 1;
            else
                _assert(status != _PS_ALIVE);
            end
            counter.number_of_alive = counter.number_of_alive - 1;
            _assert(counter.number_of_released == counter.number_of_alive  +
                                                  counter.number_of_killed +
                                                  counter.number_of_exited);
        end
    end
end

#endif
