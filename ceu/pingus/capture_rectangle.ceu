#ifndef _CAPTURERECTANGLE_CEU
#define _CAPTURERECTANGLE_CEU

native do
    ##include "pingus/globals.hpp"
    ##include "pingus/fonts.hpp"
end

class CaptureRectangle with
    var IPlayfield& playfield;
do
    pool SpritePingu[1] good_or_bad;
    pool SpritePingu[1] left_or_right;

    var char[] action_str;

    var Vector3f pos_gb = Vector3f(0,0,1000);
    var Vector3f pos_lr = Vector3f(0,0,1000);

    par do
        every WORLD_UPDATE do
            if this.playfield.current_pingu? then
                var IPingu&& pingu = this.playfield.current_pingu!;
                pos_gb.x = pingu:get_center_pos().x;
                pos_gb.y = pingu:get_center_pos().y;
                pos_lr.x = pingu:get_center_pos().x;
                pos_lr.y = pingu:get_center_pos().y + 2;
            end
        end
    with
        var SpritePingu&&? sprite_gb;
        var SpritePingu&&? sprite_lr;
        every this.playfield.ok_pingu do

            if sprite_gb? then
                kill *sprite_gb!;
            end
            if sprite_lr? then
                kill *sprite_lr!;
            end

            if not this.playfield.current_pingu? then
                continue;
            end

            var IPingu&& pingu = this.playfield.current_pingu!;

            if {globals::developer_mode} then
                action_str = [] .. action_str .. " Id: ";
                action_str = [] .. action_str ..
                    (char&&)_XXX_PURE({StringUtil::to_string}(pingu:me.get_id()).c_str());
            end

            var int action = pingu:me.current_action;
            if not {ActionName::CATCHABLE}[action] then
                continue;
            end

            // RESET STRING
            do
                action_str = [] ..  (char&&)_XXX_PURE(pingu:me.get_name().c_str());

                if (pingu:wall_action != {ActionName::NONE} or
                    pingu:fall_action != {ActionName::NONE})
                then
                    action_str = [] .. action_str .. ['['];

                    if (pingu:wall_action != {ActionName::NONE}) then
                        var char c = {ActionName::PERSISTENT_CHAR}[pingu:wall_action];
_assert(not "Not tested yet!");
                        _assert(c!='\0' and "This is not a persitent action!");
                        action_str = [] .. action_str .. [c];
                    end

                    if (pingu:fall_action != {ActionName::NONE}) then
                        var char c = {ActionName::PERSISTENT_CHAR}[pingu:fall_action];
                        _assert(c!='\0' and "This is not a persitent action!");
                        action_str = [] .. action_str .. [c];
                    end

                    action_str = [] .. action_str .. [']'];
                end
            end

            // RESPAWN SPRITES
            do
                var bool is_left = pingu:direction == _LEFT;

                sprite_gb =
                    spawn SpritePingu in good_or_bad with
                        this.x = &pos_gb.x;
                        this.y = &pos_gb.y;

                        if {ActionName::CHANGE_ALLOWED}[action]
                                                       [outer.playfield.me.session:get_action_name()]
                        then
                            this.name = "game/cursors/capgood";
                        else
                            this.name = "game/cursors/capbad";
                        end;
                    end;

                sprite_lr =
                    spawn SpritePingu in left_or_right with
                        this.x = &pos_lr.x;
                        this.y = &pos_lr.y;

                        if is_left then
                            this.name = "game/cursors/arrow_left";
                        else
                            this.name = "game/cursors/arrow_right";
                        end
                    end;
            end
        end
    with
        every sc in WORLD_DRAW do
            if not this.playfield.current_pingu? then
                continue;
            end
            var IPingu&& pingu = this.playfield.current_pingu!;

            if ({ActionName::CATCHABLE}[pingu:me.current_action]) then
                sc:color().print_center({Fonts::courier_small},
                            _Vector2i((int)(pingu:get_center_pos().x),
                                      (int)(pingu:get_center_pos().y - 46)),
                            &&action_str,
                            1000)
                    finalize with nothing; end; // print_center() is @nohold
            end
        end
    end
end

#endif
