#ifndef _CAPTURERECTANGLE_CEU
#define _CAPTURERECTANGLE_CEU

native do
    ##include "pingus/fonts.hpp"
end

class CaptureRectangle with
    var IPlayfield& playfield;
    var _CaptureRectangle& me;
do
    pool SpritePingu[1] good_or_bad;
    pool SpritePingu[1] left_or_right;

    var Vector3f pos_gb = Vector3f(0,0,1000);
    var Vector3f pos_lr = Vector3f(0,0,1000);

    par do
        every WORLD_UPDATE do
            if this.playfield.current_pingu? then
                var IPingu&& pingu = this.playfield.current_pingu!;
                pos_gb.x = pingu:me.get_center_pos().x;
                pos_gb.y = pingu:me.get_center_pos().y;
                pos_lr.x = pingu:me.get_center_pos().x;
                pos_lr.y = pingu:me.get_center_pos().y + 2;
            end
        end
    with
        var SpritePingu&&? sprite_gb;
        var SpritePingu&&? sprite_lr;
        every this.playfield.ok_pingu do

            if sprite_gb? then
                kill *sprite_gb!;
            end
            if sprite_lr? then
                kill *sprite_lr!;
            end

            if not this.playfield.current_pingu? then
                continue;
            end

            var IPingu&& pingu = this.playfield.current_pingu!;

            var int action = pingu:me.current_action;
            if not {ActionName::CATCHABLE}[action] then
                continue;
            end

            var bool is_left = pingu:me.direction.is_left();

            sprite_gb =
                spawn SpritePingu in good_or_bad with
                    this.x = &pos_gb.x;
                    this.y = &pos_gb.y;

                    if {ActionName::CHANGE_ALLOWED}[action]
                                                   [outer.playfield.me.session:get_action_name()]
                    then
                        this.name = "game/cursors/capgood";
                    else
                        this.name = "game/cursors/capbad";
                    end;
                end;

            sprite_lr =
                spawn SpritePingu in left_or_right with
                    this.x = &pos_lr.x;
                    this.y = &pos_lr.y;

                    if is_left then
                        this.name = "game/cursors/arrow_left";
                    else
                        this.name = "game/cursors/arrow_right";
                    end
                end;
        end
    with
        every sc in WORLD_DRAW do
            if not this.playfield.current_pingu? then
                continue;
            end
            var IPingu&& pingu = this.playfield.current_pingu!;

            if ({ActionName::CATCHABLE}[pingu:me.current_action]) then
                sc:color().print_center({Fonts::courier_small},
                            _Vector2i((int)(pingu:me.get_center_pos().x),
                                      (int)(pingu:me.get_center_pos().y - 46)),
                            pingu:me.get_name(),
                            1000);
            end
        end
    end
end

#endif
