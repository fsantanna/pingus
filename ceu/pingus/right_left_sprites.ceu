#ifndef _RIGHT_LEFT_SPRITES_CEU
#define _RIGHT_LEFT_SPRITES_CEU

#include "../engine/display/sprite.ceu"

// TODO: get by ref

class RightLeftSprites with
    var IPositionDirection& parent;
    var char[]& left;
    var char[]& right;
    var char[] none;
    var bool is_world = true;
    pool Sprite[1] sprites;
    function (void)=>Sprite&& get;
do
    event void e_change;
    par do
        var int dir = this.parent.direction;
        every DELTA do
            if this.parent.direction != dir then
                dir = this.parent.direction;
                emit e_change;
            end
        end
    with
        loop do
            var Sprite&&? sprite =
                spawn Sprite in this.sprites with
                    this.is_world = outer.is_world;
                    this.x = &_XXX_NOHOLD(&&outer.parent.pos_x);
                    this.y = &_XXX_NOHOLD(&&outer.parent.pos_y);
                    if outer.parent.direction == _NONE then
                        this.name = _XXX_PURE(&&outer.none);
                    else/if outer.parent.direction == _LEFT then
                        this.name = _XXX_PURE(&&outer.left);
                    else
                        this.name = _XXX_PURE(&&outer.right);
                    end;
                end;
            par/or do
                await *sprite!;
                escape 0;
            with
                await e_change;
            end
            kill *sprite!;
        end
    end

    /* PUBLIC */

    function (void)=>Sprite&& get do
        loop s in this.sprites do
            return s;
        end
        _assert(0);
    end
end

#endif
