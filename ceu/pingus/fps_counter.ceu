#ifndef _FPS_COUNTER_CEU
#define _FPS_COUNTER_CEU

native/pre do
    ##include <string>
    ##include <sstream>
    static std::string fps_string;
end

class FPSCounter with
do
    var int count = 0;
    par do
        loop do
            watching 1s do
                every DELTA do
                    count = count + 1;
                end
            end
            {
                {
                    std::ostringstream str;
                    str << THIS(CEU_FPSCounter)->count << " fps";
                    fps_string = str.str();
                }
            };
            count = 0;
        end
    with
        loop do
            await REDRAW;
            if {globals::print_fps} then
                call {Fonts::pingus_small}.render(
                    _origin_top_center,
                    {Display::get_width()}/2, 35,
                    _fps_string,
                    *{Display::s_framebuffer}
                );
                await REDRAW;
                call {Fonts::pingus_small}.render(
                    _origin_top_center,
                    {Display::get_width()}/2, 35,
                    { "+ " + fps_string + " +"},
                    *{Display::s_framebuffer}
                );
            end
        end
    end
end

#endif
