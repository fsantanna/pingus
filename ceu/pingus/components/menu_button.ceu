#ifndef _MENUBUTTON_CEU
#define _MENUBUTTON_CEU

#include "../../engine/display/sprite.ceu"
#include "../../engine/gui/rect_component.ceu"

class MenuButton with
    var float x, y;
    var char[] text, hint;
    event void ok_clicked;

    function (float x, float y, _char&& text, _char&& hint)=>MenuButton build;
do
    function (float x, float y, _char&& text, _char&& hint)=>MenuButton build do
        this.x    = x;
        this.y    = y;
        this.text = []..text;
        this.hint = []..hint;   // TODO: not used anylonger?
    end

    var Rect r1 = Rect(x, y, 0, 0);
    var Sprite bg = Sprite.build_name(&r1, "core/menu/menuitem");

    var Rect r2 = Rect(x-bg.get_width()/2, y-bg.get_height()/2,
                       bg.get_width(), bg.get_height());
    var RectComponent component with
        this.rect = &r2;
    end;

    par do
        every component.on_click do
            emit this.ok_clicked;
        end
    with
        loop do
            await component.on_pointer_enter;
            {Sound::PingusSound::play_sound ("tick");};
            do
                var Sprite _ = Sprite.build_name(&r1, "core/menu/menuitem_highlight");
                await component.on_pointer_leave;
            end
        end
    with
        every REDRAW do
            call {Fonts::chalk_large}.render(
                _origin_top_center,
                r1.left,
                r1.top - 28,
                (_char&&)&&this.text,
                *{Display::s_framebuffer}
            ) finalize with end;
        end
    end
end

#endif

