#ifndef _MENUBUTTON_CEU
#define _MENUBUTTON_CEU

#include "../../engine/display/sprite.ceu"
#include "../../engine/gui/rect_component.ceu"

code/await MenuButton (var int x, var int y, var _char_const_ptr text, var _char_const_ptr hint)
                        -> (event& void ok_clicked)
                            -> FOREVER
do
    event void ok_clicked_;
    ok_clicked = &ok_clicked_;

    var Rect r1 = val Rect(x, y, 0, 0);
    var& ISprite bg;
    spawn Sprite_from_name(&r1, "core/menu/menuitem", false) -> (&bg);

    var Rect r2 = val Rect(x-bg.frame_size.width/2, y-bg.frame_size.height/2,
                           bg.frame_size.width, bg.frame_size.height);

    var& IRectComponent component;
    spawn RectComponent(&r2) -> (&component);

    par do
        every component.on_click do
            emit ok_clicked_;
        end
    with
        loop do
            await component.on_pointer_enter;
            {Sound::PingusSound::play_sound ("tick");};
            do
                spawn Sprite_from_name(&r1, "core/menu/menuitem_highlight", false);
                await component.on_pointer_leave;
            end
        end
    with
        every SDL_REDRAW do
            call {Fonts::chalk_large}.render(
                _origin_top_center,
                r1.left,
                r1.top - 28,
                text,
                *{Display::s_framebuffer}
            );
        end
    end
end

#endif

