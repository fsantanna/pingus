#ifndef _MENUBUTTON_CEU
#define _MENUBUTTON_CEU

#include "../../engine/display/sprite.ceu"
#include "../../engine/gui/rect_component.ceu"

class MenuButton with
    var float x, y;
    var char[] text, hint;
    event bool ok_clicked;

    function (float x, float y, char&& text, char&& hint)=>MenuButton build;
do
    function (float x, float y, char&& text, char&& hint)=>MenuButton build do
        this.x    = x;
        this.y    = y;
        this.text = []..text;
        this.hint = []..hint;   // TODO: not used anylonger?
    end

    var Sprite bg = Sprite.w_xy_name(false, &x,&y, "core/menu/menuitem");

    var Rect rect = Rect(x-bg.get_width()/2, y-bg.get_height()/2,
                         bg.get_width(), bg.get_height());
    var RectComponent component with
        this.rect = &rect;
    end;

    par/or do
        await component.on_click;
    with
        loop do
            await component.on_pointer_enter;
            {Sound::PingusSound::play_sound ("tick");};
            do
                var Sprite _ = Sprite.w_xy_name(false, &x,&y,
                                    "core/menu/menuitem_highlight");
                await component.on_pointer_leave;
            end
        end
    with
        every gc in SCREENMANAGER_DRAW do
            gc:print_center({Fonts::chalk_large},
                            _Vector2i(x, y-28),
                            &&this.text)
                finalize with end;
        end
    end

    escape 0;
end

#endif

