#ifdef _CHECKBOX_CEU
#error multiple include
#else
#define _CHECKBOX_CEU

data ICheckBox with
    var   bool is_on;
    event bool go_click;
end

code/await CheckBox (var& Rect rect, var bool is_on)
                        -> (var& ICheckBox checkbox)
                            -> FOREVER
do
    var ICheckBox checkbox_ = val ICheckBox(is_on,_);
    checkbox = &checkbox_;

    var& IRectComponent comp;
    spawn RectComponent(&rect) -> (&comp);

    par do
        every comp.on_primary_button_pressed do
            emit checkbox_.go_click(not checkbox_.is_on);
        end
    with
        loop do
            var _char_const_ptr file;
            if checkbox_.is_on then
                file = "core/menu/checkbox_marked_small";
            else
                file = "core/menu/checkbox_small";
            end
            spawn Sprite_from_name(&rect, file, false);
            checkbox_.is_on = await checkbox_.go_click;
        end
    end
end

#endif

