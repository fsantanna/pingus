#ifndef _CHECKBOX_CEU
#define _CHECKBOX_CEU

class CheckBox with
    var Rect rect;
    var bool is_on;
    function (Rect rect, bool is_on)=>CheckBox build;
    event bool ok_clicked;
do
    function (Rect rect, bool is_on)=>CheckBox build do
        this.rect  = rect;
        this.is_on = is_on;
    end

    var RectComponent component with
        this.rect = &outer.rect;
    end;

    loop do
        if this.is_on then
            var Sprite _ = Sprite.build_name(&rect, "core/menu/checkbox_marked_small");
            await component.on_primary_button_pressed;
            is_on = false;
        else
            var Sprite _ = Sprite.build_name(&rect, "core/menu/checkbox_small");
            await component.on_primary_button_pressed;
            is_on = true;
        end
        emit ok_clicked => is_on;
    end
end

#endif

