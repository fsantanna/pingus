#ifndef _ACTIONBUTTON_CEU
#define _ACTIONBUTTON_CEU

#include "../../engine/gui/rect_component.ceu"

class ArmageddonButton with
    var Rect& rect;
    function (Rect& rect)=>ArmageddonButton build;
do
    function (Rect& rect)=>ArmageddonButton build do
        this.rect = &rect;
    end

    this.rect.width  = 38;
    this.rect.height = 60;
    var RectComponent component = RectComponent.build(&outer.rect);

    do
        var SpriteR _ = SpriteR.build_name(&outer.rect,
                                           "core/buttons/hbuttonbgb");
        var SpriteR s = SpriteR.build_name(&outer.rect,
                                           "core/buttons/armageddon_anim");
        s.frame_delay = 0;
        s.set_frame(7);

        loop do
            await component.on_primary_button_pressed;
            watching 1s do
                await component.on_primary_button_pressed;
                break;
            end
        end
    end

    do
        var SpriteR _ = SpriteR.build_name(&outer.rect,
                                           "core/buttons/hbuttonbgb");
        var SpriteR _ = SpriteR.build_name(&outer.rect,
                                           "core/buttons/armageddon_anim");
        emit global:go_armageddon;
        await FOREVER;
    end
end

class ForwardButton with
    var int x;
    var int y;
    function (int x, int y)=>ForwardButton build;
do
    function (int x, int y)=>ForwardButton build do
        this.x = x;
        this.y = y;
    end

    var Rect rect = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &rect;
        //this.state = &global:world!:get_state();
    end;

    var float xf = this.x;
    var float yf = this.y;
    spawn do
        loop do
            do
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbgb";
                end;
                await component.on_primary_button_pressed;
            end
            emit global:go_forward => true;
            do
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbg";
                end;
                par/or do
                    await ON_PRIMARY_BUTTON_RELEASED;
                with
                    var int v = await global:go_forward
                                until v == false;
                end
            end
            emit global:go_forward => false;
        end
    end

    var Sprite _ with
        this.is_world = false;
        this.x    = &xf;
        this.y    = &yf;
        this.name = "core/buttons/fast_forward";
    end;
    await FOREVER;
end

class PauseButton with
    var int x;
    var int y;
    function (int x, int y)=>PauseButton build;
do
    function (int x, int y)=>PauseButton build do
        this.x = x;
        this.y = y;
    end

    var Rect rect = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &rect;
        //this.state = &global:world!:get_state();
    end;

    var float xf = this.x;
    var float yf = this.y;
    spawn do
        loop do
            var bool from_out = true;
            par/or do
                await global:go_pause_toggle until from_out;
            with
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbgb";
                end;
                await component.on_primary_button_pressed;
                from_out = false;
                emit global:go_pause_toggle;
            end

            from_out = true;
            par/or do
                await global:go_pause_toggle until from_out;
            with
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbg";
                end;
                await component.on_primary_button_pressed;
                from_out = false;
                emit global:go_pause_toggle;
            end
        end
    end

    var Sprite _ with
        this.is_world = false;
        this.x    = &xf;
        this.y    = &yf;
        this.name = "core/buttons/pause";
    end;
    await FOREVER;
end

#endif
