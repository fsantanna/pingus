#ifndef _ACTIONBUTTON_CEU
#define _ACTIONBUTTON_CEU

#include "../../engine/gui/rect_component.ceu"

class ArmageddonButton with
    var int x;
    var int y;
    event void ok;
do
    var Rect rect = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &rect;
        //this.state = &global:world!:get_state();
    end;

    var float xf = this.x;
    var float yf = this.y;
    do
        var Sprite _ with
            this.is_world = false;
            this.x    = &xf;
            this.y    = &yf;
            this.name = "core/buttons/hbuttonbgb";
        end;
        var Sprite sprite with
            this.is_world = false;
            this.x    = &xf;
            this.y    = &yf;
            this.name = "core/buttons/armageddon_anim";
        end;
        sprite.frame_delay = 0;
        sprite.set_frame(7);

        loop do
            await component.on_primary_button_pressed;
            watching 1s do
                await component.on_primary_button_pressed;
                break;
            end
        end
    end

    do
        var Sprite _ with
            this.is_world = false;
            this.x    = &xf;
            this.y    = &yf;
            this.name = "core/buttons/hbuttonbg";
        end;
        var Sprite _ with
            this.is_world = false;
            this.x    = &xf;
            this.y    = &yf;
            this.name = "core/buttons/armageddon_anim";
        end;
        emit global:go_armageddon;
        await FOREVER;
    end
end

class ForwardButton with
    var int x;
    var int y;
do
    var Rect rect = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &rect;
        //this.state = &global:world!:get_state();
    end;

    var float xf = this.x;
    var float yf = this.y;
    spawn do
        loop do
            do
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbgb";
                end;
                await component.on_primary_button_pressed;
            end
            emit global:go_forward => true;
            do
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbg";
                end;
                await ON_PRIMARY_BUTTON_RELEASED;
            end
            emit global:go_forward => false;
        end
    end

    var Sprite _ with
        this.is_world = false;
        this.x    = &xf;
        this.y    = &yf;
        this.name = "core/buttons/fast_forward";
    end;
    await FOREVER;
end

class PauseButton with
    var int x;
    var int y;
do
    var Rect rect = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &rect;
        //this.state = &global:world!:get_state();
    end;

    var float xf = this.x;
    var float yf = this.y;
    spawn do
        loop do
            var bool from_out = true;
            par/or do
                await global:go_pause_toggle until from_out;
            with
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbgb";
                end;
                await component.on_primary_button_pressed;
                from_out = false;
                emit global:go_pause_toggle;
            end

            from_out = true;
            par/or do
                await global:go_pause_toggle until from_out;
            with
                var Sprite _ with
                    this.is_world = false;
                    this.x    = &xf;
                    this.y    = &yf;
                    this.name = "core/buttons/hbuttonbg";
                end;
                await component.on_primary_button_pressed;
                from_out = false;
                emit global:go_pause_toggle;
            end
        end
    end

    var Sprite _ with
        this.is_world = false;
        this.x    = &xf;
        this.y    = &yf;
        this.name = "core/buttons/pause";
    end;
    await FOREVER;
end

#endif
