#ifndef _ACTIONBUTTON_CEU
#define _ACTIONBUTTON_CEU

#include "../../engine/gui/rect_component.ceu"

class ArmageddonButton with
    var Rect& rect;
    function (Rect& rect)=>ArmageddonButton build;
do
    function (Rect& rect)=>ArmageddonButton build do
        this.rect = &rect;
    end

    this.rect.width  = 38;
    this.rect.height = 60;
    var RectComponent component = RectComponent.build(&outer.rect);

    do
        var Sprite _ = Sprite.build_nameX(&outer.rect, "core/buttons/hbuttonbgb");
        var Sprite s = Sprite.build_nameX(&outer.rect, "core/buttons/armageddon_anim");
        s.frame_delay = 0;
        s.set_frame(7);

        loop do
            await component.on_primary_button_pressed;
            watching 1s do
                await component.on_primary_button_pressed;
                break;
            end
        end
    end

    do
        var Sprite _ = Sprite.build_nameX(&outer.rect, "core/buttons/hbuttonbgb");
        var Sprite _ = Sprite.build_nameX(&outer.rect, "core/buttons/armageddon_anim");
        emit global:go_armageddon;
        await FOREVER;
    end
end

class ForwardButton with
    var int x;
    var int y;
    function (int x, int y)=>ForwardButton build;
do
    function (int x, int y)=>ForwardButton build do
        this.x = x;
        this.y = y;
    end

    var Rect r1 = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &r1;
    end;

    spawn do
        var Rect r2 = Rect(this.x, this.y, 0, 0);
        loop do
            do
                var Sprite _ = Sprite.build_nameX(&r2, "core/buttons/hbuttonbgb");
                await component.on_primary_button_pressed;
            end
            emit global:go_forward => true;
            do
                var Sprite _ = Sprite.build_nameX(&r2, "core/buttons/hbuttonbg");
                par/or do
                    await ON_PRIMARY_BUTTON_RELEASED;
                with
                    var int v = await global:go_forward
                                until v == false;
                end
            end
            emit global:go_forward => false;
        end
    end

    var Rect r3 = Rect(this.x,this.y, 0,0);
    var Sprite _ = Sprite.build_nameX(&r3, "core/buttons/fast_forward");
    await FOREVER;
end

class PauseButton with
    var int x;
    var int y;
    function (int x, int y)=>PauseButton build;
do
    function (int x, int y)=>PauseButton build do
        this.x = x;
        this.y = y;
    end

    var Rect r1 = Rect(this.x,this.y, 38,60);
    var RectComponent component with
        this.rect  = &r1;
    end;

    spawn do
        var Rect r2 = Rect(this.x, this.y, 0, 0);
        loop do
            var bool from_out = true;
            par/or do
                await global:go_pause_toggle until from_out;
            with
                var Sprite _ = Sprite.build_nameX(&r2, "core/buttons/hbuttonbgb");
                await component.on_primary_button_pressed;
                from_out = false;
                emit global:go_pause_toggle;
            end

            from_out = true;
            par/or do
                await global:go_pause_toggle until from_out;
            with
                var Sprite _ = Sprite.build_nameX(&r2, "core/buttons/hbuttonbg");
                await component.on_primary_button_pressed;
                from_out = false;
                emit global:go_pause_toggle;
            end
        end
    end

    var Rect r3 = Rect(this.x, this.y, 0, 0);
    var Sprite _ = Sprite.build_nameX(&r3, "core/buttons/pause");
    await FOREVER;
end

#endif
