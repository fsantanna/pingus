#ifndef _HOTSPOT_CEU
#define _HOTSPOT_CEU

class Hotspot with
    var _FileReader&& reader;

    function (void)       =>float     get_z_pos;
    function (_Vector3f&&)=>void      set_pos;
    function (void)       =>_Vector3f get_pos;
do
    var Vector3f pos;
    { static Vector3f p; };
    this.reader:read_vector("position", _p);
    this.pos = Vector3f(_p.x,_p.y,_p.z);

    var float para;
    this.reader:read_float("parallax", para);

    /* Background image */
    // FIXME: would be nice to allow surface manipulation with
    // animated sprites, but it's not that easy to do
    { static ResDescriptor desc; };
    this.reader:read_desc("surface", _desc);
    var _Sprite&? sprite_;
    finalize
        sprite_ = &{new Sprite}(_desc);
    with
        //_delete(&&sprite_!);
    end
    var _Sprite& sprite = &sprite_!;

    par do
        every WORLD_UPDATE do
            this.sprite.update();
        end
    with
        every gc in WORLD_DRAW do
            // FIXME: para support doesnn't work correctly
            gc:color().draw(this.sprite, _Vector3f(this.pos.x,
                                                   this.pos.y,
                                                   this.pos.z));
        end
    end

    /* PUBLIC */

    function (void)=>float get_z_pos do
        return this.pos.z;
    end
    function (_Vector3f&& p)=>void set_pos do
        this.pos = Vector3f(p:x, p:y, p:z);
    end
    function (void)=>_Vector3f get_pos do
        return _Vector3f(this.pos.x, this.pos.y, this.pos.z);
    end
end

#endif
