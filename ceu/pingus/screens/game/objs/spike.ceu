#ifdef _SPIKE_CEU
#error multiple include
#else
#define _SPIKE_CEU

code/await Spike (void) -> FOREVER
do
    {
        static Vector3f p;
        WORLDOBJ_FACTORY_READER.read_vector("position", p);
    }
    var Vector2i pst = val Vector2i({p.x}, {p.y});

    loop do
        loop do
            await WORLD_UPDATE;
            var&? Pingu p;
            loop p in outer.pingus do
                if (p!.pingu.rect.left > pst.x+16-5 and
                    p!.pingu.rect.left < pst.x+16+5 and
                    p!.pingu.rect.top  > pst.y      and
                    p!.pingu.rect.top  < pst.y + 32)
                then
                    var Rect r = val Rect(pst.x,pst.y, 0,0);
                    var&? Sprite_from_name s = spawn Sprite_from_name(&r, "traps/spike", true);
                    watching s, p do
                        loop do
                            await WORLD_UPDATE;
                            if (s!.sprite.frame == 3 and
                                p!.pingu.rect.left > pst.x+16-12 and
                                p!.pingu.rect.left < pst.x+16+12 and
                                p!.pingu.rect.top  > pst.y       and
                                p!.pingu.rect.top  < pst.y+32)
                            then
                                break;
                            end
                        end
                        emit p!.pingu.go_action({ActionName::DEAD});
                        await FOREVER;
                    end
                end
            end
        end
    end

#if 0
    /* PUBLIC */

    function (void)=>float get_z_pos do
        return pst.z;
    end
    function (_Vector3f&& p)=>void set_pos do
        pst = Vector3f(p:x, p:y, p:z);
    end
    function (void)=>_Vector3f get_pos do
        return _Vector3f(pst.x, pst.y, pst.z);
    end
#endif
end

#endif
