#ifdef _SMASHER_CEU
#error multiple include
#else
#define _SMASHER_CEU

code/await Smasher (void) -> FOREVER
do
    {
        static Vector3f p;
        WORLDOBJ_FACTORY_READER.read_vector("position", p);
    }
    var Vector3f pst = val Vector3f({p.x}, {p.y}, {p.z});
    do
        _log_info("Drawing colmap entry");
        {
            DO
                CollisionMask buf("traps/smasher_cmap");
        }
        call Game_Put({&buf}, pst.x as int, pst.y as int, {Groundtype::GP_SOLID});
        {
            END
        }
    end

    var Rect rect = val Rect(pst.x as int,pst.y as int, 0,0);
    loop do
        var int ok = do
            var&? Sprite_from_name sprite =
                spawn Sprite_from_name(&rect,"traps/smasher",true);
            _ceu_dbg_assert(call Get_Frame_Count(&sprite!.sprite) == 6);
            sprite!.sprite.frame_delay = 0;

            loop do
                await WORLD_UPDATE;

                var&? Pingu pingu;
                loop pingu in outer.pingus do
                    // Activate the smasher if a Pingu is under it
                    if ((pingu!.pingu.direction == {LEFT}    and
                         pingu!.pingu.rect.left > pst.x + 65 and
                         pingu!.pingu.rect.left < pst.x + 85)
                    or
                        (pingu!.pingu.direction == {RIGHT}    and
                         pingu!.pingu.rect.left > pst.x + 190 and
                         pingu!.pingu.rect.left < pst.x + 210))
                    then
                        if (pingu!.pingu.current_action != {ActionName::SPLASHED}) then
                            escape 1;
                        end
                    end
                end
            end
        end;

        do
            var&? Sprite_from_name sprite =
                spawn Sprite_from_name(&rect,"traps/smasher",true);

            do
                var int i;
                loop i in [0 -> 6[ do
                    await WORLD_UPDATE;
                    sprite!.sprite.frame = i;
                end
            end

            // SMASH!!! The thing hitten earth and kills the pingus
            call {Sound::PingusSound::play_sound}("tenton");

            do
                var int i;
                loop i in [0 -> 20[ do
                    spawn SmokeParticle((pst.x + 20 + (_rand() % 260)) as int,
                                        (pst.y + 180) as int,
                                        {Math::frand()-0.5f},
                                        {Math::frand()-0.5f})
                            in outer.smoke_particles;
                end
            end

            var&? Pingu pingu;
            loop pingu in outer.pingus do
                if (call pingu!.Is_Inside((pst.x +  30) as int,
                                          (pst.y +  90) as int,
                                          (pst.x + 250) as int,
                                          (pst.y + 190) as int))
                then
                    emit pingu!.pingu.go_action({ActionName::SPLASHED});
                end
            end

            do
                var int i;
                loop i in [0 <- 5] do
                    await WORLD_UPDATE;
                    sprite!.sprite.frame = i;
                end
            end
        end
    end

    /* PUBLIC */

#if 0
    function (void)=>float get_z_pos do
        return this.pos.z;
    end
    function (_Vector3f&& p)=>void set_pos do
        this.pos = Vector3f(p:x, p:y, p:z);
    end
    function (void)=>_Vector3f get_pos do
        return _Vector3f(this.pos.x, this.pos.y, this.pos.z);
    end
#endif
end

#endif
