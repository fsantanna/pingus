#ifdef _CAPTURERECTANGLE_CEU
#error multiple include
#else
#define _CAPTURERECTANGLE_CEU

var&? Pingu old;
loop do
    await outer.main.dt;

    var float min_dist = 500.0;
    var&? Pingu nxt;
    var&? Pingu p;

    loop p in outer.pingus do
        if call/recursive p!.Pingu_Is_Over(outer.mouse_pos.x,outer.mouse_pos.y) then
            var float dist = call/recursive p!.Pingu_Dist(outer.mouse_pos.x, outer.mouse_pos.y);
            if dist < min_dist then
                min_dist = dist;
                nxt = &p;
            end
        end
    end

    if nxt? then
        if old? then
            if (&&nxt! != &&old!) then
                emit old!.pingu.go_toggle_capture;
                emit nxt!.pingu.go_toggle_capture;
            else
                // still the same pingu
            end
        else
            emit nxt!.pingu.go_toggle_capture;
        end
        old = &nxt;
    else/if old? then
        emit old!.pingu.go_toggle_capture;
        old = nil;
    end
end

#endif
