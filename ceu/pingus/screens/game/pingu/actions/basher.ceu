#ifdef _BASHER_CEU
#error multiple include
#else
#define _BASHER_CEU

/* Defines the minimum "wall" height needed for a bash to happen. */
#define BASHER_MIN_BASH_HEIGHT 5

/* Defines the maximum height up to which a check is made to see if there
   is anything to bash.  Best to make this at least (min_bash_height +
   pingu_height). */
#define BASHER_MAX_BASH_HEIGHT 33

/* Defines the maximum no. of steps down a Basher can go down before it
   stops being a Basher and turns into a Faller. */
#define BASHER_MAX_STEPS_DOWN 3

code/await Basher (void) -> _ActionName__Enum
do
    vector[] byte left  = [] .. "pingus/player"
                          .. (call Pingu_Get_Owner_Str() as _char&&)
                          .. "/basher/left";
    vector[] byte right = [] .. "pingus/player"
                          .. (call Pingu_Get_Owner_Str() as _char&&)
                          .. "/basher/right";

    var&? RightLeftSprites sprites =
        spawn RightLeftSprites(&outer.pingu.rect, &outer.pingu.direction, &left, &left, &right);

    var int? bash_reach; // set in bash

    code/tight Bash                  (void) -> void;
    code/tight Walk_Forward          (void) -> bool;
    code/tight Have_Something_To_Dig (void) -> bool;

    // Start a bash even so the action will stops instantly after the first bash
    call Bash();

    /* UPDATE */
    var bool is_first_bash = true;
    var int i;
    loop i do
        await outer.game.dt;
        await outer.game.dt;
        await outer.game.dt;

        if not call Walk_Forward() then
            escape {ActionName::FALLER};
        end

        // If walking on to something (i.e. hasn't fallen)
        if call Pingu_Rel_Getpixel(0,-1) != {Groundtype::GP_NOTHING} then
            // If the Basher has walked into something that it won't be 
            // able to bash
            if call Pingu_Rel_Getpixel(0,0)              == {Groundtype::GP_SOLID} or
               call Pingu_Rel_Getpixel(0,{pingu_height}) == {Groundtype::GP_SOLID}
            then
                // Change direction and let walk code walk forward/up to get out.
                { Sound::PingusSound::play_sound("chink"); };
                call Pingu_Toggle_Direction();
                escape {ActionName::WALKER};
            else/if is_first_bash or call Have_Something_To_Dig() then
                is_first_bash = false;
                // We only bash every second step, cause the Pingus would
                // get trapped otherwise in the bashing area.
                if i%2 == 1 then
                    call Bash();
                end
            else
/// TODO
                var&? Sprite_from_name s;
                loop s in sprites!.sprites do
                    break;
                end
                if ((s!.sprite.frame as float) /
                     (call Get_Frame_Count(&s!.sprite))) > 0.6
                // FIXME: Game logic must be separate from Sprite
                // FIXME: EVIL! Engine must not relay on graphic
                then
                    escape {ActionName::WALKER};
                end
            end
        end
    end

    /* PRIVATE */

    code/tight Bash (void) -> void do
        {
            static CollisionMask bash_radius(
                    "pingus/common/bash_radius_gfx",
                    "pingus/common/bash_radius");
            ceu_dbg_assert(bash_radius.get_width() % 2 == 0);
        };
        outer.bash_reach = {bash_radius.get_width()};

        call Game_Remove({&bash_radius},
                         outer.pingu.rect.off.x - {bash_radius.get_width() / 2},
                         outer.pingu.rect.off.y - {bash_radius.get_height()} + 1);
    end

    code/tight Walk_Forward (void) -> bool do

        // Find the correct y position to go to next
        var int y_inc = do
            var int i;
            loop i in [0 -> BASHER_MAX_STEPS_DOWN] do
                // If there is something below, get out of this loop
                if call Pingu_Rel_Getpixel(0,-i-1) != {Groundtype::GP_NOTHING} then
                    escape -i;
                end
            end
            escape -BASHER_MAX_STEPS_DOWN-1;
        end;

        if y_inc < -BASHER_MAX_STEPS_DOWN then
            // The step down is too much.  So stop being a Basher and be a 
            // Faller.
            escape false;
        else
            // Note that Pingu::set_pos() is the 'reverse' of the y co-ords of
            // rel_getpixel()
            outer.pingu.rect.off.x = outer.pingu.rect.off.x + (outer.pingu.direction as int);
            outer.pingu.rect.off.y = outer.pingu.rect.off.y - y_inc;
            emit outer.pingu.rect.changed;
        end
        escape true;
    end

    code/tight Have_Something_To_Dig (void) -> bool do
        // Check that there is something "within" the Basher's reach
        var int x;
        loop x in [0 -> outer.bash_reach!] do
            var int y;
            loop y in [BASHER_MIN_BASH_HEIGHT -> BASHER_MAX_BASH_HEIGHT] do
                if call Pingu_Rel_Getpixel(x, y) == {Groundtype::GP_GROUND} then
                    _log_debug("Basher: Found something to dig...");
                    escape true;
                end
            end
        end
        escape false;
    end
end

#endif
