#ifdef _BLOCKER_CEU
#error multiple include
#else
#define _BLOCKER_CEU

code/await Blocker (void) -> _ActionName__Enum
do
    vector[] byte left  = [] .. "pingus/player"
                          .. (call Pingu_Get_Owner_Str() as _char&&)
                          .. "/blocker/left";
    vector[] byte right = [] .. "pingus/player"
                          .. (call Pingu_Get_Owner_Str() as _char&&)
                          .. "/blocker/right";

    spawn RightLeftSprites(&outer.pingu.rect, &outer.pingu.direction, &left, &right, true);

    code/tight Standing_On_Ground (void) -> bool do
        escape (call Pingu_Rel_Getpixel(0,-1) != {Groundtype::GP_NOTHING});
    end

    code/tight (var& IPingu target)=>void catch_pingu do
        if &&target != &&outer.pingu then // avoid 'self' catch
            if ( target.rect.left > outer.pingu.rect.left-16
            and  target.rect.left < outer.pingu.rect.left+16
            and  target.rect.top  > outer.pingu.rect.top-32
            and  target.rect.top  < outer.pingu.rect.top+5 )
            then
                if (target.rect.left > outer.pingu.rect.left) then
                    target.direction = {RIGHT};
                else
                    target.direction = {LEFT};
                end
            end
        end
    end

    /* UPDATE */
    loop do
        await WORLD_UPDATE;
        if not call Standing_On_Ground() then
            escape {ActionName::FALLER};
        end

        // FIXME: PinguHolder iterations should be handled otherwise
        var&? Pingu p;
        loop p in outer.pingus do
            call Catch_Pingu(&p);
        end
    end
end

#endif
