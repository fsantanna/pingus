#ifdef _WALKER_CEU
#error multiple include
#else
#define _WALKER_CEU

#define WALKER_MAX_STEPS 5  // max nr. of pixels that pingu can walk up/down

code/await Walker (void) -> _ActionName__Enum
do
    vector[] byte left1  = [] .. "pingus/player"
                           .. (call Pingu_Get_Owner_Str() as _char&&)
                           .. "/walker/left";
    vector[] byte right1 = [] .. "pingus/player"
                           .. (call Pingu_Get_Owner_Str() as _char&&)
                           .. "/walker/right";

    spawn RightLeftSprites(&outer.pingu.rect, &outer.pingu.direction, &left1, &left1, &right1);

    // Reset the velocity
    var Vector3f v = val Vector3f(0, 0, 0);
    call Pingu_Set_Velocity(v);

    /* UPDATE */
    par do
        await outer.game.dt
        until (outer.fall_action == {ActionName::FLOATER});
        vector[] byte left2  = [] .. "other/floaterlayer/left";
        vector[] byte right2 = [] .. "other/floaterlayer/right";
        spawn RightLeftSprites(&outer.pingu.rect, &outer.pingu.direction, &left2, &left2, &right2);
        await FOREVER;
    with
        loop do
            await outer.game.dt;

            var int old_x = outer.pingu.rect.x;
            var int old_y = outer.pingu.rect.y;

              /* How should this code work?

                 1) Check that the Pingu stands still on ground, if not turn it into
                 a faller or drown. The reason we do so, is that we catch situations
                 where a digger or a similar action removed the ground under the
                 walker.

                 2) If pingu is still on ground, we can preprare the next step

                 3) Check if up-hill or down-hill is required

                 4)

              */

            if call Pingu_Rel_Getpixel(0,-1) == {Groundtype::GP_NOTHING} then
                // The Pingu stands no longer on ground, the cause for this could 
                // be a digger, miner or a bomber
                // We search for the nearest ground below the pingu, if we can't 
                // find anything within a few pixels, we will turn into a faller
                var int i = _;
                var bool? found_ground = do
                    loop i in [-4 <- -2] do
                        if not (call Pingu_Rel_Getpixel(0,i) == {Groundtype::GP_NOTHING}) then
                            escape true;
                        end
                    end
                end;
                if found_ground? then
                    outer.pingu.rect.y = outer.pingu.rect.y - i;
                else
                    escape {ActionName::FALLER};
                end
            end

            // FIXME: here we could/should scan more pixels
            if call Pingu_Rel_Getpixel(1, 0) == {Groundtype::GP_BRIDGE} and
               not call Pingu_Head_Collision_On_Walk(1, 1)
            then // bridge
                // simple, stupid, but working bridge code
                // FIXME: We don't check if we 'drift' into a solid ground block
                outer.pingu.rect.x = outer.pingu.rect.x + (outer.pingu.direction as int);
                outer.pingu.rect.y = outer.pingu.rect.y - 1; // pingus 'float' through bridges
            else
                // Non of the trivial moves worked, so we do up-hill or down-hill 
                // walking
                // FIXME: currently the pingu takes multiple steps at once when
                // FIXME: working uphill, this looks kind of ugly

                // FIXME: rel_getpixel works on the current pos, so modifing pos
                // FIXME: is evil, a backup copy might help

                // if infront is a pixel Pingu is walking up the mountain
                // we can continue walking up. search for the correct y_pos
                var int possible_y_step = 0;
                var bool found_next_step = false;
                var int y_inc;
                loop y_inc in [-WALKER_MAX_STEPS -> +WALKER_MAX_STEPS] do
                    // up/down-hill scan
                    if (call Pingu_Rel_Getpixel(1, y_inc)=={Groundtype::GP_NOTHING} or
                        call Pingu_Rel_Getpixel(1, y_inc)=={Groundtype::GP_BRIDGE})
                    and
                        // FIXME: This causes a rather huge step
                        call Pingu_Rel_Getpixel(1, y_inc - 1) != {Groundtype::GP_NOTHING}
                    then
                        // FIXME:
                        found_next_step = true;
                        possible_y_step = y_inc;
                        // No break here, since we always want to use the 
                        // highest possible position
                        //break;
                    end
                end

                if found_next_step then
                    // pos.y has a reversed co-system to rel_getpixel()?
                    outer.pingu.rect.x = outer.pingu.rect.x + (outer.pingu.direction as int);
                    outer.pingu.rect.y = outer.pingu.rect.y - possible_y_step;
                else
                    if call Pingu_Rel_Getpixel(1,0) != {Groundtype::GP_NOTHING} then
                        // We reached a wall
                        var _ActionName__Enum wall_action = outer.wall_action;
                        if wall_action != {ActionName::NONE} then
                            _log_debug("Pingu: We are in front of a wall, setting persistant action");
                            escape wall_action;
                        end

                        // No persitent action found, so change the direction
                        call Pingu_Toggle_Direction();
                    else
                        // We take the step, so that we are in the air
                        outer.pingu.rect.x = outer.pingu.rect.x + (outer.pingu.direction as int);
                        // We reached a cliff
                        escape {ActionName::FALLER};
                    end
                end

                // This is moved here to fix the bug where pingu stuck turning both 
                // sides indefinetely when a head collision occured. the fix needs 
                // the above downhill walk being done before head collision check.
                if call Pingu_Head_Collision_On_Walk(0, 0) then
                    _log_debug("Pingu: Head collision");

                    //if the new position causes a head collision, we are already 
                    //stuck in a wall, so lets go back to the old position
                    call Pingu_Toggle_Direction();
                    outer.pingu.rect.x = old_x;
                    outer.pingu.rect.y = old_y;
                end
            end
        end
    end
end

#endif
