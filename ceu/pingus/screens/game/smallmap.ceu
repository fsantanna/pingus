#ifdef _SMALLMAP_CEU
#error multiple include
#else
#define _SMALLMAP_CEU

code/tight Is_At (var int x, var int y) -> bool do
    escape (x > outer.game.rect_smallmap.x and
            x < outer.game.rect_smallmap.x + outer.game.rect_smallmap.w and
            y > outer.game.rect_smallmap.y and
            y < outer.game.rect_smallmap.y + outer.game.rect_smallmap.h);
end

par do
    do ()
        #include "smallmap_image.ceu"
    end
with
    loop do
        var int x;
        var int y;
        (x,y) = await ON_PRIMARY_BUTTON_PRESSED until call Is_At(x,y);
        watching ON_PRIMARY_BUTTON_RELEASED do
            loop do
                // set view to the given COs
                var int cx = (x - outer.game.rect_smallmap.x) * outer.game.rect.w / outer.game.rect_smallmap.w;
                var int cy = (y - outer.game.rect_smallmap.y) * outer.game.rect.h / outer.game.rect_smallmap.h;
                call Game_Set_Viewpoint(cx, cy);
                (x,y) = await ON_POINTER_MOVE until call Is_At(x,y);
            end
        end
    end
with
    every outer.main.redraw do
        var Vector2i vec = call Game_Get_Pos();
        {
            static Rect view_rect;
        };

        if (outer.game.rect.w > {Display::get_width()}) then
            var int rwidth = {Display::get_width()} * outer.game.rect_smallmap.w / outer.game.rect.w;
            {
                view_rect.left = @(outer.game.rect_smallmap.x + (vec.x * outer.game.rect_smallmap.w / outer.game.rect.w) - rwidth/2);
                view_rect.right = view_rect.left + @rwidth;
            }
        else
            {
                view_rect.left  = @outer.game.rect_smallmap.x;
                view_rect.right = @(outer.game.rect_smallmap.x + outer.game.rect_smallmap.w);
            }
        end

        if (outer.game.rect.h > {Display::get_height()}) then
            var int rheight = {Display::get_height()} * outer.game.rect_smallmap.h / outer.game.rect.h;
            {
                view_rect.top = @(outer.game.rect_smallmap.y + (vec.y * outer.game.rect_smallmap.h / outer.game.rect.h) - rheight/2);
                view_rect.bottom = view_rect.top + @rheight;
            }
        else
            {
                view_rect.top    = @outer.game.rect_smallmap.y;
                view_rect.bottom = @(outer.game.rect_smallmap.y + outer.game.rect_smallmap.h);
            }
        end

        {Display::s_framebuffer->draw_rect(view_rect, Color(255,255,0));}

        // Draw Pingus
        var&? Pingu pingu;
        loop pingu in outer.pingus do
            var int x = outer.game.rect_smallmap.x + (pingu!.pingu.rect.x * outer.game.rect_smallmap.w  / outer.game.rect.w);
            var int y = outer.game.rect_smallmap.y  + (pingu!.pingu.rect.y  * outer.game.rect_smallmap.h / outer.game.rect.h);
            {Display::s_framebuffer->draw_line(Vector2i(@x, @y), Vector2i(@x, @y-2), Color(255,255,0));}
        end
    end
end

#endif
