#ifdef _MENUBUTTON_CEU
#error multiple include
#else
#define _MENUBUTTON_CEU

#include "engine/display/spr.ceu"
#include "engine/gui/rct_component.ceu"

code/await MenuButton (var int x, var int y, var _char_const_ptr text, var _char_const_ptr hint)
                        -> (event void ok_clicked)
                            -> FOREVER
do
    var Rct r1 = val Rct(x, y, 0, 0, &outer.main.ctr);
    var&? Spr_from_name bg =
        spawn Spr_from_name(&r1, "core/menu/menuitem", &outer.main.dt, &outer.main.redraw);

    var Rct r2 = val Rct(x-bg!.sprite.frame_size.width/2, y-bg!.sprite.frame_size.height/2,
                           bg!.sprite.frame_size.width, bg!.sprite.frame_size.height,
                         &outer.main.ctr);

    var& RctComponent component = spawn RctComponent(&r2);

    par do
        every component.component.on_click do
            emit ok_clicked;
        end
    with
        loop do
            await component.component.on_pointer_enter;
            {Sound::PingusSound::play_sound ("tick");};
            do
                spawn Spr_from_name(&r1, "core/menu/menuitem_highlight", &outer.main.dt, &outer.main.redraw);
                await component.component.on_pointer_leave;
            end
        end
    with
        every outer.main.redraw do
            var Rct r = _;
            call/recursive Rct_Off(&r1, &r);
            call {Fonts::chalk_large}.render(
                _origin_top_center,
                r.x,
                r.y - 28,
                text,
                *{Display::s_framebuffer}
            );
        end
    end
end

#endif

