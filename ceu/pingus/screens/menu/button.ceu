#ifdef _MENUBUTTON_CEU
#error multiple include
#else
#define _MENUBUTTON_CEU

code/await MenuButton (var int x, var int y, var _char_const_ptr text, var _char_const_ptr hint)
                        -> (event void ok_clicked)
                            -> FOREVER
do
    var Rect r1 = val Rect(x, y, 0, 0);
    var&? Sprite_from_name bg =
        spawn Sprite_from_name(&r1, "core/menu/menuitem", &outer.main.dt, &outer.main.redraw, false);

    var Rect r2 = val Rect(x-bg!.sprite.frame_size.width/2, y-bg!.sprite.frame_size.height/2,
                           bg!.sprite.frame_size.width, bg!.sprite.frame_size.height);

    var& RectComponent component = spawn RectComponent(&r2);

    par do
        every component.component.on_click do
            emit ok_clicked;
        end
    with
        loop do
            await component.component.on_pointer_enter;
            {Sound::PingusSound::play_sound ("tick");};
            do
                spawn Sprite_from_name(&r1, "core/menu/menuitem_highlight", &outer.main.dt, &outer.main.redraw, false);
                await component.component.on_pointer_leave;
            end
        end
    with
        every outer.main.redraw do
            call {Fonts::chalk_large}.render(
                _origin_top_center,
                r1.left,
                r1.top - 28,
                text,
                *{Display::s_framebuffer}
            );
        end
    end
end

#endif

