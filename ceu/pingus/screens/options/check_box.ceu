#ifdef _CHECKBOX_CEU
#error multiple include
#else
#define _CHECKBOX_CEU

data ICheckBox with
    var   bool is_on;
    event bool go_click;
end

code/await CheckBox (var& Rect rect, var bool is_on)
                        -> (var ICheckBox checkbox)
                            -> FOREVER
do
    checkbox = val ICheckBox(is_on,_);

    var&? RectComponent component = spawn RectComponent(&rect);

    par do
        every component!.component.on_primary_button_pressed do
            emit checkbox.go_click(not checkbox.is_on);
        end
    with
        loop do
            var _char_const_ptr file;
            if checkbox.is_on then
                file = "core/menu/checkbox_marked_small";
            else
                file = "core/menu/checkbox_small";
            end
            spawn Sprite_from_name(&rect, file, &outer.main.dt, &outer.main.redraw, false);
            checkbox.is_on = await checkbox.go_click;
        end
    end
end

#endif

