#ifndef _LASEREXIT_CEU
#define _LASEREXIT_CEU

class LaserExit with
    pool IPingu[]& pingus;
    var _FileReader&& reader;

    function (void)       =>float     get_z_pos;
    function (_Vector3f&&)=>void      set_pos;
    function (void)       =>_Vector3f get_pos;
do
    var Vector3f pos;
    { static Vector3f p; };
    this.reader:read_vector("position", _p);
    this.pos = Vector3f(_p.x,_p.y,_p.z);

    loop do
        var Rect r = Rect(pos.x,pos.y, 0,0);
        do
            var Sprite sprite = Sprite.build_name(&r, "traps/laser_exit");
            sprite.frame_delay = 0;

            var IPingu&& pingu = do
                loop do
                    await WORLD_UPDATE;
                    var IPingu&&? pingu;
                    loop p in this.pingus do
                        if (p:get_x() < this.pos.x+34+10 and
                            p:get_x() > this.pos.x+34    and
                            p:get_y() < this.pos.y+43+20 and
                            p:get_y() > this.pos.y+43)
                        then
                            escape p;
                        end
                    end
                end
            end;
            emit pingu:go_action => {ActionName::LASERKILL};
        end

        do Sprite.build_name(&r, "traps/laser_exit");
   end

    /* PUBLIC */

    function (void)=>float get_z_pos do
        return this.pos.z;
    end
    function (_Vector3f&& p)=>void set_pos do
        this.pos = Vector3f(p:x, p:y, p:z);
    end
    function (void)=>_Vector3f get_pos do
        return _Vector3f(this.pos.x, this.pos.y, this.pos.z);
    end
end

#endif
