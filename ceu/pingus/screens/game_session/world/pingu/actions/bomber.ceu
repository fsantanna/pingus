#ifndef _BOMBER_CEU
#define _BOMBER_CEU

native/pre do
    ##include "../src/pingus/colliders/pingu_collider.hpp"
    ##include "../src/pingus/pingu_enums.hpp"
end

#include "../movers/linear_mover.ceu"

class Bomber with
    interface IPinguAction;
do
    var char[] bomber = [] .. "pingus/player"
                           .. (_char&&)this.pingu.get_owner_str().c_str()
                           .. "/bomber/left";
    var Sprite sprite = Sprite.build_name(&outer.pingu.rect, (_char&&)&&bomber);

    var char[] explo = [] .. "pingus/player"
                          ..  (_char&&)this.pingu.get_owner_str().c_str()
                          .. "/explo";
    {
        ##include "../../../src/pingus/collision_mask.hpp"
        static CollisionMask bomber_radius(
                "other/bomber_radius_gfx",
                "other/bomber_radius"
        );
    };

    call {Sound::PingusSound::play_sound}("ohno", 0.5, 0.0);

    par do
        // TODO: does it make any difference??
        do LinearMover with
            this.pingu = &outer.pingu;
        end;
    with
        /* UPDATE */

        // SOUND
        await WORLD_UPDATE until sprite.get_current_frame() == 10;
        call {Sound::PingusSound::play_sound}("plop", 0.5, 0.0);

        // PARTICLES, TERRAIN, EXPLOSION

        await WORLD_UPDATE until sprite.get_current_frame() == 13;
        emit global:go_create_pingu_particles => (this.pingu.get_x(),
                                                  this.pingu.get_y()-5);

        global:world!:remove(
            &&_bomber_radius,
            this.pingu.get_x() - _bomber_radius.get_width()/2,
            this.pingu.get_y() - _bomber_radius.get_width()/2 - 16
        );

        do
            var Sprite s = Sprite.build_name(&outer.pingu.rect, (_char&&)&&explo);
            s.offset.x = 32;
            s.offset.y = 48;
            await WORLD_UPDATE;
        end

        // ANIMATION END
        await sprite;
        escape {ActionName::DEAD};
    end
end

#endif
