#ifndef _LINEARMOVER_CEU
#define _LINEARMOVER_CEU

native/pre do
    ##include "../src/pingus/colliders/pingu_collider.hpp"
end

code/await Mover (void) -> (event& void ok_collided) -> FOREVER
do
    event void ok_collided_;
    ok_collided = &ok_collided_;

    every WORLD_UPDATE do
        var bool collided = false;

        var Vector3f vel = call Get_Velocity();
        var Vector3f pst = _;

        // enclosing block for the C++ declarations
        {{
            Vector3f pst, move;
            pst.x = @(call Get_X());
            pst.y = @(call Get_Y());

            move = Vector3f(@vel.x,@vel.y,@vel.z);

            Vector3f target_pos = pst + move;
            Vector3f step_vector = move;

            // Static cast to stop warning
            int move_length = (int)(move.length());

            // Make the step vector (i.e. change to a unit vector)
            step_vector.normalize();

            // Move to the destination one unit vector at a time
            for (int i=0; i<move_length; i++) {
                @collided = Colliders::PinguCollider()(@outer.gfx_map.get_colmap(), pst, step_vector);
                pst = pst + step_vector;
                if (@collided) {
                    break;
                }
            }

            // If on a collision pixel, back away from it.
            if (@collided) {
                pst = pst - step_vector;
            }

            @pst.x = pst.x;
            @pst.y = pst.y;
            @pst.z = pst.z;
        }};

        call Set_Pos(pst.x, pst.y);
        if collided then
            emit ok_collided_;
        end
    end
end

#endif
