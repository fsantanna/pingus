#ifndef _PINGUSMENU_CEU
#define _PINGUSMENU_CEU

#include "../../engine/display/sprite.ceu"
#include "../components/menu_button.ceu"

class PingusMenu with
do
    var Size size = Size({Display::get_width()}, {Display::get_height()});

    var float x = size.width  / 2;
    var float y = size.height / 2;
    var Sprite logo = Sprite.w_xy_name(false, &x,&y, "core/misc/logo");
        x = x - logo.get_width()/2;
        y = y - 280;

    {Sound::PingusSound::play_music("pingus-1.it");};

    par do
        do MenuButton.build(size.width/2 - 125,
                            size.height/2 - 20,
                            "Story", "");
        escape true;
    with
        do MenuButton.build(size.width/2 + 125,
                            size.height/2 - 20,
                            "Editor", "");
    with
        do MenuButton.build(size.width/2 - 125,
                            size.height/2 + 50,
                            "Options", "");
    with
        do MenuButton.build(size.width/2 + 125,
                            size.height/2 + 50,
                            "Levelsets", "");
    with
        do MenuButton.build(size.width/2,
                            size.height/2 + 120,
                            "Exit", "");
        escape false;
    with
        every gc in SCREENMANAGER_DRAW do
            gc:print_left({Fonts::pingus_small},
                          _Vector2i(gc:get_width()/2 - 400 + 25, gc:get_height()-140),
            {
"Pingus " VERSION " - Copyright (C) 1998-2011 Ingo Ruhnke <grumbel@gmail.com>\n"
"See the file AUTHORS for a complete list of contributors.\n"
"Pingus comes with ABSOLUTELY NO WARRANTY. This is free software, and you are\n"
            "welcome to redistribute it under certain conditions; see the file COPYING for details.\n"
            });

            gc:draw_fillrect(_Rect(0,
                                   {Display::get_height()} - 26,
                                   {Display::get_width ()},
                                   {Display::get_height()}),
                             _Color(0, 0, 0, 255));
            gc:print_center({Fonts::pingus_small},
                             _Vector2i(gc:get_width() / 2,
                                       gc:get_height() - {Fonts::pingus_small.get_height()} - 8),
"..:: Ctrl-g: mouse grab   ::   F10: fps counter   ::   F11: fullscreen   ::   F12: screenshot ::..");
        end
    end
end

#endif
