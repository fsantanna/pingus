#ifndef _JUMPER_CEU
#define _JUMPER_CEU

#include "../right_left_sprites.ceu"

class Jumper with
    interface IPinguAction;
do
    var char[] left  = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/jumper/left";
    var char[] right = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/jumper/right";
    var RightLeftSprites _ with
        this.pingu = &outer.pingu;
        this.left  = &left;
        this.right = &right;
    end;

    await WORLD_UPDATE;

    // if climber, do a wall-jump, else just jump forward
    if this.previous_action == {ActionName::CLIMBER} then
        this.pingu.me.direction.change();
    end

    var float dx, dy;
    if this.pingu.me.direction.is_left() then
        dx = -4.58;
        dy = -4.58;
    else // if (this.pingu.me.direction.is_right())
        dx =  4.58;
        dy = -4.58;
    end
    var Vector3f velocity = this.pingu.get_velocity();
    velocity.x = velocity.x + dx;
    velocity.y = velocity.y + dy;
    this.pingu.set_velocity(velocity);

    // Move the me.pingu:n the air, so that it can start 'falling'
    this.pingu.me.set_y(this.pingu.me.get_y() - 1);

    escape {ActionName::FALLER};
end

#endif
