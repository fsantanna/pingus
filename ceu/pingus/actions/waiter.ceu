#ifndef _WAITER_CEU
#define _WAITER_CEU

#include "../pingu_action.ceu"
#include "../right_left_sprites.ceu"

class Waiter with
    interface IPinguAction;
do
    var char[] str = [] .. "pingus/player"
                        .. (char&&)*this.pingu.me.get_owner_str().c_str()
                        .. "/waiter/left";
    var Sprite sprite with
        this.me = &_XXX_PTR2REF(null);
        this.xxx_from_ceu = true;
        this.name = _XXX_PURE(&&str);
    end;

    par do
        /* UPDATE */
        var float countdown = 2.0;
        loop do
            AWAIT_UNTIL_PINGU(PINGU_UPDATE);
            sprite.me.update();
            if countdown < 0 then
                escape {ActionName::WALKER};
            end
            countdown = countdown - 0.025;
        end
    with
        /* DRAW */
        every (me_,gc) in PINGU_DRAW do
            if me_ != &&this.pingu.me then
                continue;
            end
            _GC_COLOR_DRAW(gc, sprite.me,
                               this.pingu.me.get_pos());
        end
    end

    /* PUBLIC */

    function (_ActionName__Enum action_name) => bool change_allowed do
        return true;
    end
end

#endif
