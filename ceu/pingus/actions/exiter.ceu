#ifndef _EXITER_CEU
#define _EXITER_CEU

#include "../pingu_action.ceu"
#include "../right_left_sprites.ceu"

class Exiter with
    interface IPinguAction;
do
    var char[] left  = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/exit/left";
    var char[] right = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/exit/right";
    var RightLeftSprites sprites with
        this.pingu = &outer.pingu;
        this.left  = &left;
        this.right = &right;
    end;

    { Sound::PingusSound::play_sound("yipee"); };

    /* UPDATE */
    loop do
        await PINGUHOLDER_UPDATE;
        if sprites.get():is_finished() then
            escape {ActionName::NONE};
        end
    end
end

#endif
