#ifndef _EXITER_CEU
#define _EXITER_CEU

#include "../pingu_action.ceu"
#include "../right_left_sprites.ceu"

class Exiter with
    interface IPinguAction;
do
    var char[] left  = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/exit/left";
    var char[] right = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/exit/right";
    var RightLeftSprites sprites with
        this.left  = &left;
        this.right = &right;
    end;

    par do
        every (me_,gc) in PINGU_DRAW do
            if me_ != &&this.pingu.me then
                continue;
            end
            _GC_COLOR_DRAW(gc, sprites.get(this.pingu.me.direction).me,
                               this.pingu.me.get_pos());
        end
    with
        var bool sound_played = false;
        loop do
            AWAIT_UNTIL_PINGU(PINGU_UPDATE);
            sprites.get(this.pingu.me.direction).me.update();

            if not sound_played then
                { Sound::PingusSound::play_sound("yipee"); };
                sound_played = true;
            end

            if sprites.get(this.pingu.me.direction).me.is_finished() then
                escape {ActionName::NONE};
            end
        end
    end

    /* PUBLIC */

    function (_ActionName__Enum action_name) => bool change_allowed do
        return true;
    end
end

#endif
