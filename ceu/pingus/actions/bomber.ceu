#ifndef _BOMBER_CEU
#define _BOMBER_CEU

native/pre do
    ##include "../src/pingus/colliders/pingu_collider.hpp"
    ##include "../src/pingus/pingu_enums.hpp"
end

#include "../right_left_sprites.ceu"
#include "../movers/linear_mover.ceu"

class Bomber with
    interface IPinguAction;
do
    var char[] left  = [] .. "pingus/player"
                          .. (char&&)this.pingu.get_owner_str().c_str()
                          .. "/bomber/left";
    var char[] right = [] .. "pingus/player"
                          .. (char&&)this.pingu.get_owner_str().c_str()
                          .. "/bomber/right";
    var RightLeftSprites sprites with
        this.pingu = &outer.pingu;
        this.left  = &left;
        this.right = &right;
    end;

    {
        static Vector3f vec1;
    };
    _vec1.x = this.pingu.get_pos().x;
    _vec1.y = this.pingu.get_pos().y;

    global:world!:play_sound("ohno", _vec1, 0.5);

    par do
        // TODO: does it make any difference??
        do LinearMover with
            this.pingu = &outer.pingu;
        end;
    with
        /* UPDATE */
        // while the Bomber hasn't 'exploded' yet
        await WORLD_UPDATE
        until sprites.get():get_current_frame() == 10;

        // Throwing particles
        {
            static Vector3f vec2;
        };
        _vec2.x = this.pingu.get_pos().x;
        _vec2.y = this.pingu.get_pos().y;

        global:world!:play_sound("plop", _vec2, 0.5);

        await WORLD_UPDATE
        until sprites.get():get_current_frame() == 13;

        emit global:world!:create_pingu_particles => (this.pingu.get_x(),
                                                      this.pingu.get_y()-5);

        {
            ##include "../../../src/pingus/collision_mask.hpp"
            static CollisionMask bomber_radius(
                    "other/bomber_radius_gfx",
                    "other/bomber_radius"
            );
        };

        global:world!:remove(
                &&_bomber_radius,
                (int) ((int)(this.pingu.get_x()) 
                                - (_bomber_radius.get_width()/2)),
                (int) ((int)(this.pingu.get_y()) -
                                16 - (_bomber_radius.get_width()/2)));

        do
            var char[] str = [] .. "pingus/player"
                                ..  (char&&)this.pingu.get_owner_str().c_str()
                                .. "/explo";
            var Sprite _ with
                this.x     = &_XXX_NOHOLD(&&outer.pingu.pos_x);
                this.y     = &_XXX_NOHOLD(&&outer.pingu.pos_y);
                this.name  = _XXX_PURE(&&str);
            end;
            await WORLD_UPDATE;
        end

        // The pingu explode
        await sprites;
        escape {ActionName::DEAD};
    end
end

#endif
