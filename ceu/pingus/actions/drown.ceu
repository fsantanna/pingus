#ifndef _DROWN_CEU
#define _DROWN_CEU

#include "../pingu_action.ceu"
#include "../right_left_sprites.ceu"

class Drown with
    interface IPinguAction;
do
    var char[] left  = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/drownfall/left";
    var char[] right = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/drownfall/right";
    var RightLeftSprites sprites with
        this.pingu = &outer.pingu;
        this.left  = &left;
        this.right = &right;
    end;

    /* UPDATE */
    loop do
        await PINGUHOLDER_UPDATE;
        if sprites.get():is_finished() then
            this.pingu.me.set_status({Pingu::PS_DEAD});
            escape {ActionName::DEAD};
        end
    end
end

#endif
