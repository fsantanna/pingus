#ifndef _DROWN_CEU
#define _DROWN_CEU

#include "../pingu_action.ceu"
#include "../right_left_sprites.ceu"

class Drown with
    interface IPinguAction;
do
    var char[] left  = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/drownfall/left";
    var char[] right = [] .. "pingus/player"
                          .. (char&&)*this.pingu.me.get_owner_str().c_str()
                          .. "/drownfall/right";
    var RightLeftSprites sprites with
        this.left  = &left;
        this.right = &right;
    end;

    par do
        every (me_,gc) in PINGU_DRAW do
            if me_ != &&this.pingu.me then
                continue;
            end
            _GC_COLOR_DRAW(gc, sprites.get(this.pingu.me.direction).me,
                               this.pingu.me.get_pos());
        end
    with
        loop do
            AWAIT_UNTIL_PINGU(PINGU_UPDATE);
            sprites.get(this.pingu.me.direction).me.update();
            if sprites.get(this.pingu.me.direction).me.is_finished() then
                this.pingu.me.set_status({Pingu::PS_DEAD});
                escape {ActionName::DEAD};
            end
        end
    end
end

#endif
