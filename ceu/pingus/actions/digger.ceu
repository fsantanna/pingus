#ifndef _DIGGER_CEU
#define _DIGGER_CEU

native/pre do
    ##include "../src/pingus/pingu.hpp"
    ##include "../src/pingus/pingu_action.hpp"
    ##include "../src/pingus/actions/digger.hpp"
    ##define Actions__Digger Actions::Digger
end
native @plain _Actions__Digger;

input (_Actions__Digger&&) DIGGER_UPDATE;

class Digger with
    var _Actions__Digger& me;
do
    var int delay_count = 0;
    loop do
        AWAIT_UNTIL_MYSELF(_Actions__Digger,DIGGER_UPDATE);
        me.sprite.update();
        delay_count = delay_count + 1;

        if me.rel_getpixel(0,-1) == {Groundtype::GP_WATER} or
           me.rel_getpixel(0,-1) == {Groundtype::GP_LAVA}
        then
            escape {ActionName::DROWN};
        else/if not me.have_something_to_dig() then
            me.dig(true);
            escape {ActionName::WALKER};
        else/if delay_count%4 == 0 then
            me.dig(false);
        end
    end
end

#endif
