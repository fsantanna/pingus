#ifdef _SURFACE_BUTTON_CEU
#error multiple include
#else
#define _SURFACE_BUTTON_CEU

code/await SurfaceButton (var& Rect rect,
                          var _char_const_ptr button,
                          var _char_const_ptr button_pressed,
                          var _char_const_ptr button_over)
                            -> (var& IRectComponent component)
                                -> FOREVER
do
    var&? RectComponent component_ = spawn RectComponent(&rect);
    watching component_ do
        component = &component_!.component;

        loop do
            do
                spawn Sprite_from_name(&rect, button, _);
                await component.on_pointer_enter;
            end
            watching component.on_pointer_leave do
                loop do
                    do
                        spawn Sprite_from_name(&rect, button_over, _);
                        await component.on_primary_button_pressed;
                    end
                    spawn Sprite_from_name(&rect, button_pressed, false);
                    await ON_PRIMARY_BUTTON_RELEASED;
                end
            end
        end
    end
end

#endif
