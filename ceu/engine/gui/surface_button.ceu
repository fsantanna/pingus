#ifndef _SURFACE_BUTTON_CEU
#define _SURFACE_BUTTON_CEU

#include "../display/sprite.ceu"
#include "rect_component.ceu"

code/await SurfaceButton (var& Rect rect,
                          var _char_const_ptr button,
                          var _char_const_ptr button_pressed,
                          var _char_const_ptr button_over)
                            -> (var& IRectComponent component)
                                -> FOREVER
do
    var& IRectComponent component_;
    spawn RectComponent(&rect) -> (&component_);
    component = &component_;

    loop do
        do
            spawn Sprite_from_name(_, &rect, button);
            await component_.on_pointer_enter;
        end
        watching component_.on_pointer_leave do
            loop do
                do
                    spawn Sprite_from_name(_, &rect, button_over);
                    await component_.on_primary_button_pressed;
                end
                var Sprite sprite = _;
                spawn Sprite_from_name(_, &rect, button_pressed);
                await ON_PRIMARY_BUTTON_RELEASED;
            end
        end
    end
end

#endif
