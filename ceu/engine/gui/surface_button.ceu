#ifndef _SURFACE_BUTTON_CEU
#define _SURFACE_BUTTON_CEU

#include "../display/sprite.ceu"
#include "rect_component.ceu"

class SurfaceButton with
    var float x;
    var float y;
    var char&& button;
    var char&& button_pressed;
    var char&& button_over;

    function (void)=>RectComponent& get_component;
do
    var char[] b         = []..button;
    var char[] b_pressed = []..button_pressed;
    var char[] b_over    = []..button_over;

    var Rect rect = Rect(x, y, 0, 0);
    var Sprite sprite = Sprite.build_name(&rect, _XXX_PURE(&&b));
    var RectComponent component with
        this.rect = &rect;
    end;
    kill sprite;

    loop do
        do
            var Sprite _ = Sprite.build_name(&rect, _XXX_PURE(&&b));
            await component.on_pointer_enter;
        end
        watching component.on_pointer_leave do
            loop do
                do
                    var Sprite _ = Sprite.build_name(&rect, _XXX_PURE(&&b_over));
                    await component.on_primary_button_pressed;
                end
                var Sprite _ = Sprite.build_name(&rect, _XXX_PURE(&&b_pressed));
                await ON_PRIMARY_BUTTON_RELEASED;
            end
        end
    end

    function (void)=>RectComponent& get_component do
        return &this.component;
    end
end

#endif
