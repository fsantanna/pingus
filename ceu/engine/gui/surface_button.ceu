#ifndef _SURFACE_BUTTON_CEU
#define _SURFACE_BUTTON_CEU

#include "../display/sprite.ceu"
#include "rect_component.ceu"

class SurfaceButton with
    var float x;
    var float y;
    var _char&& button;
    var _char&& button_pressed;
    var _char&& button_over;
output:
    var RectComponent& component;
do
    var char[] b         = []..button;
    var char[] b_pressed = []..button_pressed;
    var char[] b_over    = []..button_over;

    var Rect rect = Rect(x, y, 0, 0);
    var RectComponent component_ with
        this.rect = &rect;
    end;
    this.component = &component_;
    var Sprite sprite = Sprite.build_name(&rect, (_char&&)&&b);
    kill sprite;

    loop do
        do
            var Sprite _ = Sprite.build_name(&rect, (_char&&)&&b);
            await component.on_pointer_enter;
        end
        watching component.on_pointer_leave do
            loop do
                do
                    var Sprite _ = Sprite.build_name(&rect, (_char&&)&&b_over);
                    await component.on_primary_button_pressed;
                end
                var Sprite _ = Sprite.build_name(&rect, (_char&&)&&b_pressed);
                await ON_PRIMARY_BUTTON_RELEASED;
            end
        end
    end
end

#endif
