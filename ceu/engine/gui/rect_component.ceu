#ifndef _RECT_COMPONENT_CEU
#define _RECT_COMPONENT_CEU

native @plain _GraphicContextState;

class RectComponent with
    var Rect& rect;
    var _GraphicContextState&? state;

    event (int,int) on_primary_button_pressed;
    event void      on_pointer_enter;
    event void      on_pointer_leave;
    event void      on_click;

    function (int x, int y)=>bool is_at;
do
    par do
        loop do
            var int x, y;
            (x,y) = await ON_PRIMARY_BUTTON_PRESSED
                    until this.is_at(x,y);
            emit this.on_primary_button_pressed => (x,y);
            (x,y) = await ON_PRIMARY_BUTTON_RELEASED;
            if this.is_at(x,y) then
                emit this.on_click;
            end
        end
    with
        var int is = false;
        every (x,y) in ON_POINTER_MOVE do
            if this.is_at(x,y) != is then
                is = not is;
                if is then
                    emit this.on_pointer_enter;
                else
                    emit this.on_pointer_leave;
                end
            end
        end
    end

    function (int x, int y)=>bool is_at do
        if this.state? then
            x = this.state!.screen2world(_Vector2i(x,y)).x;
            y = this.state!.screen2world(_Vector2i(x,y)).y;
        end

        return rect.left<=x and x<=rect.left+rect.width
           and rect.top <=y and y<=rect.top+rect.height;
    end
end

#endif
