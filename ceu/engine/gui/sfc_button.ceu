#ifdef _SFC_BUTTON_CEU
#error multiple include
#else
#define _SFC_BUTTON_CEU

code/await SfcButton (var& Rct rct,
                          var _char_const_ptr button,
                          var _char_const_ptr button_pressed,
                          var _char_const_ptr button_over)
                            -> (var& IRctComponent component)
                                -> FOREVER
do
    var& RctComponent component_ = spawn RctComponent(&rct);
    component = &component_.component;
    loop do
        do
            spawn Spr_from_name(&rct, button, &outer.main.dt, &outer.main.redraw);
            await component.on_pointer_enter;
        end
        watching component.on_pointer_leave do
            loop do
                do
                    spawn Spr_from_name(&rct, button_over, &outer.main.dt, &outer.main.redraw);
                    await component.on_primary_button_pressed;
                end
                spawn Spr_from_name(&rct, button_pressed, &outer.main.dt, &outer.main.redraw);
                await ON_PRIMARY_BUTTON_RELEASED;
            end
        end
    end
end

#endif
